# ns 和 def

cora 中的模块实现方式，是通过对符号加前缀来区分模块。

我们引入两个宏 ns 和 def：

```
(ns "this/pkg"
	(import "path/to/my/pkg")
	
	(def foo 3)
	(hello))
```

每个 ns 都要记录下自己 import 了哪些模块，比如这里的 (import "path/to/my/pkg") 就是 import 了 "path/to/my/pkg"。
一个 ns 可能引入多个模块，引入的模块列表都需要记录下来。

对于 ns 里面的每个符号，我们都要解析出它实际表示的语义是什么。它有可能是一个词法环境的变量，也有可能是全局符号。词法环境的变量比较明确，不用特殊考虑。而如果是全局符号的情况，我们就需要继续解析它所属的模块。

比如上面代码中出现的 hello，我们需要查找当前模块所有 import 的模块列表，检查 hello 是否属于某个模块的 export 符号。如果是，比如这里的 "path/to/my/pkg" 确实 (export hello) 了的，那么把 hello 改写成它的完整的符号表示 path/to/my/pkg#hello，即加上模块前缀。

对于 foo，我们查找了所有 import 的模块，并没有哪个模块 export 了 foo 这个符号，于是这种情况下我们需要给 foo 加上自身模块的前缀，也就是将 foo 变成 this/pkg#foo。

经过完整的宏展开，上面的 ns 表达式会被处理成这种形式：

```
(import "path/to/my/pkg")
(set 'this/pkg#foo 3)
(path/to/my/pkg#hello)
```

处理 ns 宏的展开：

```
(func parse
	env ns import x => x where (const? x)
	env ns import ['quote sym] => ['quote sym]
	env ns import x => (cond
						((elem? x env) x)
						((cora/init#builtin? x) x)
						(true (lookup-var x ns import))) where (symbol? x)
	env ns import ['do ..] => ..
	env ns import ['if ..] => ..
	env ns import ['lambda ..] => ..
	env ns import [f . args] => ..)
```

重点关注 where (symbol? x) 的处理分支：如果 x 能在环境中找到，说明它是词法变量，不用修改；如果 x 是 builtin 函数，也不需要额外处理；其它情况下，它是一个全局符号，需要通过 lookup-var 函数决定它的实际含义。

```
(func lookup-var
      s ns [] => (var-with-ns s ns)
      s ns [import . more] => (let export (value-or (intern (string-append import "#*ns-export*")) ())
				   (if (elem? s export)
				       (intern (string-append import (string-append "#" (symbol->string s))))
				       (lookup-var s ns more))))
```

每个模块会把它 export 的符号通过全局变量 `path/to/pkg#*ns-export*` 导出，这段代码是查找 import 的模块导出的符号:

```
(let export (value-or (intern (string-append import "#*ns-export*")) ())
```

后面就是使用 elem? 函数查询符号 s 是否是模块导出的符号，如果是，则对 s 添加上所属模块的前缀。
如果一个 import 模块里面没找到，则继续在下一个 import 模块中查找。
如果 import 都遍历完也没找到，则调用 (var-with-ns s ns)。

这个函数就是用来给符号添加模块前缀的，注意如果符号已经是带模块前缀的，不要重复添加：

```
(defun var-with-ns (var ns)
  (cond
    ((= ns "") var)
    ((symbol-cooked? var) var)
    (true (intern (string-append ns (string-append "#" (symbol->string var)))))))
```

## links
   * [目录](<SUMMARY.md>)
   * 下一节: [load 和 load-so](<08.2.md>)
