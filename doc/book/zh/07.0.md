# 元解释器

使用 cora 语言实现一个 cora 语言的解释器非常简单，只需要短短几十行代码。

对于常量的求值规则，求值得到它自身。

对于 quote 特殊表，求值后会得到一个符号。

对于一个符号，我们需要确认这个符号的语义，即区分它是一个词法作用域的变量，还是一个全局符号。
env-get 会在当前环境中查找是否存在符号该符号的绑定，如果能找到，则变量的值由环境绑定决定。
如果找不到，则将它作为全局符号处理，使用 value 函数获取该符号绑定的值。

对于 if 特殊表，先对 if 的测试条件求值，然后根据求值结果是 true 或 false，继续对 then 或者 else 分支求值。

对于 do 特殊表，依次求值即可。

```
(func eval
	  env x => x where (const? x)
	  env ['quote sym] => sym
	  env var => (let find (env-get sym env)
				   (if (null? find)
				   (value sym)
				   (cdr find))) where (symbol? var)
	  env ['if a b c] => (if (eval env a)
				 (eval env b)
				 (eval env c))
	  env ['do a b] => (do (eval env a)
			   (eval env b))
	  env ['lambda params body] => (lambda (args)
					 (let new-env (extend env params args)
					  (eval new-env body)))
	  env [f . args] => (match (map (eval env) [f . args])
				   [f1 . args1]
				   (f1 args1)))
```

最后是 lambda 和函数调用的求值规则。这里简化了一下，没有支持 curry 和 partial apply。可以留作习题由读者练习。

lambda 表达式在这里转化成了一个函数，其实无论用函数还是用其它数据结构来表示，都没有影响，它只需要把参数记录下来，以及引用的环境记录下来。等真正被执行的时候，用接收到的参数来扩展环境，然后在新的环境里面执行 lambda 的 body 部分。

对于 apply 的求值规则，先对其中第个元素分别求值，这里使用了 map (eval env)，高阶函数和柯里化这些特性都用上了。求值之后得到一个新的列表，新的列表的头节点其实就是 lambda 表达式，如果要处理 builtin function 这里会需要一些额外的处理。

一些辅助函数都比较简单，比如判断是否是 const：

```
(defun const? (x)
  (or (number? x) (string? x) (boolean? x)))
```

跟 env 相关的操作：

```
(func extend
	env [] [] => env
	env [p . params] [a . args] => (extend [(cons p a) . env] params args)
	_ _ => (error "mismatch parameters and arguments"))
	
(func env-get	
	sym [] => ()
	sym [[var . val] . _] => [var . val] where (= sym var)
	sym [_ . env] => (env-get sym env))
```

let 不是必须做成特殊表的，它可以是一个宏，我们可以在 eval 中添加这样的改写来支持 let：

```
env ['let a b c] => [['lambda [a] c] b]
```

接下来我们可以试一试我们实现的这个元解释器了。

```
(eval () "hello world") => "hello world"
(eval () 42) => 42
(eval () true) => true

(eval () 'symbol) => symbol

(eval () `(let a 3 a)) => 3

(eval () `(if true 3 4)) => 3
(eval () `(do (set 'var 123) var)) => 123

(eval () `((lambda (x) x) 42)) => 42
```


## links
   * [目录](<SUMMARY.md>)
   * 下一节: [元解释器](<08.0.md>)
